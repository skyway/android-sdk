plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
}

def SkyWayVer = rootProject.ext.SkyWayVer

android {
    compileSdk 33

    defaultConfig {
        minSdk 23
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            // local.propertiesファイルを指定
            def properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())

            def APP_ID = properties.getProperty("appId")
            buildConfigField("String", "APP_ID", "\"${APP_ID}\"")

            def SECRET_KEY = properties.getProperty("secretKey")
            buildConfigField("String", "SECRET_KEY", "\"${SECRET_KEY}\"")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }


    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "skyway-${archivesBaseName}.aar"
        }
    }

}

dependencies {
    // test
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito:mockito-core:3.+"
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"
    androidTestImplementation project(':authtoken')

    // coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"

    // JSON parser
    implementation 'com.google.code.gson:gson:2.9.1'

    // skyway-core
//    api "com.ntt.skyway:core:${SkyWayVer}"
    implementation project(":core")

    // sfu-bot
//    api "com.ntt.skyway:sfubot:${SkyWayVer}"
    implementation project(":sfubot")
}

tasks.dokkaHtml.configure {
    outputDirectory.set(file("../../../docs/room"))
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = 'https://maven.pkg.github.com/skyway/android-sdk'

            credentials {
                def localProperties = new Properties()
                localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

                username = localProperties.containsKey("com.github.user") ? localProperties.getProperty('com.github.user') : ''
                password = localProperties.containsKey("com.github.token") ? localProperties.getProperty('com.github.token') : ''
            }
        }
    }

    publications {
        GitHubPackages(MavenPublication) {
            groupId "com.ntt.skyway"
            artifactId "room"
            version SkyWayVer

            afterEvaluate {
                from components.release
            }

            pom {
                url = 'https://github.com/skyway/android-sdk'

                licenses {
                    license {
                        name = 'MIT'
                    }
                }
            }
        }
    }
}
