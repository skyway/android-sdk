plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
}

def SkyWayVer = rootProject.ext.SkyWayVer
def roomDescription = 'SkyWay Android SDK Room library'

android {
    namespace 'com.ntt.skyway.room'
    compileSdk 35

    defaultConfig {
        minSdk 23
        targetSdk 35

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            // local.propertiesファイルを指定
            def properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())

            def APP_ID = properties.getProperty("appId")
            buildConfigField("String", "APP_ID", "\"${APP_ID}\"")

            def SECRET_KEY = properties.getProperty("secretKey")
            buildConfigField("String", "SECRET_KEY", "\"${SECRET_KEY}\"")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "skyway-${archivesBaseName}.aar"
        }
    }

}

dependencies {
    // test
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito:mockito-core:3.+"
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"
    androidTestImplementation project(':authtoken')

    // coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"

    // JSON parser
    implementation 'com.google.code.gson:gson:2.9.1'

    // skyway-core
    api project(":core")

    // sfu-bot
    api project(":sfubot")
}

tasks.dokkaHtml.configure {
    outputDirectory.set(file("../../../docs/room"))
}

publishing {
    repositories {
        maven {
            name = 'MavenCentral'
            url = rootProject.layout.buildDirectory.dir('maven-central-staging-deploy')
        }
    }

    publications {
        MavenCentral(MavenPublication) {
            groupId = 'com.ntt.skyway'
            artifactId = 'room'
            version = SkyWayVer

            afterEvaluate {
                from components.release
            }

            pom {
                name = 'room'
                // the pom description is necessary for uploading aar to maven central.
                description = roomDescription
                url = 'https://github.com/skyway/android-sdk/'
                licenses {
                    license {
                        name = 'MIT'
                    }
                }
                developers {
                    developer {
                        name = 'skyway'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/skyway/android-sdk.git'
                    developerConnection = 'scm:git:ssh://github.com:skyway/android-sdk.git'
                    url = 'https://github.com/skyway/android-sdk/'
                }
            }
        }
    }
}