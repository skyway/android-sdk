plugins {
    id 'maven-publish'
}

def libwebrtcVer = rootProject.ext.libwebrtcVer
def libwebrtcDeprecation = 'SkyWay Android SDK libwebrtc library'

configurations.maybeCreate("default")
artifacts.add("default", file("libwebrtc.aar"))

tasks.register('clean') {
    // workaround for error:
    // Task with name 'clean' not found in project ':libwebrtc'
}

publishing {
    repositories {
        maven {
            name = 'MavenCentral'
            url = rootProject.layout.buildDirectory.dir('maven-central-staging-deploy')
        }
    }

    publications {
        MavenCentral(MavenPublication) {
            groupId = 'com.ntt.skyway'
            artifactId = 'libwebrtc'
            version = libwebrtcVer

            artifact("libwebrtc.aar")

            pom {
                name = 'libwebrtc'
                // the pom description is necessary for uploading aar to maven central.
                description = libwebrtcDeprecation
                url = 'https://github.com/skyway/android-sdk/'
                licenses {
                    license {
                        name = 'MIT'
                    }
                }
                developers {
                    developer {
                        name = 'skyway'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/skyway/android-sdk.git'
                    developerConnection = 'scm:git:ssh://github.com:skyway/android-sdk.git'
                    url = 'https://github.com/skyway/android-sdk/'
                }
            }
        }
    }
}
