plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

def libwebrtcVer = rootProject.ext.libwebrtcVer
def libwebrtcDeprecation = 'SkyWay Android SDK libwebrtc library'


android {
    namespace "org.webrtc"
    compileSdk 35

    defaultConfig {
        minSdk 23
        targetSdk 35
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"

        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${archivesBaseName}.aar"
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.3.0'
}

publishing {
    repositories {
        maven {
            name = 'MavenCentral'
            url = rootProject.layout.buildDirectory.dir('maven-central-staging-deploy')
        }
    }

    publications {
        MavenCentral(MavenPublication) {
            groupId = 'com.ntt.skyway'
            artifactId = 'libwebrtc'
            version = libwebrtcVer

            afterEvaluate {
                from components.release
            }

            pom {
                name = 'libwebrtc'
                // the pom description is necessary for uploading aar to maven central.
                description = libwebrtcDeprecation
                url = 'https://github.com/skyway/android-sdk/'
                licenses {
                    license {
                        name = 'MIT'
                    }
                }
                developers {
                    developer {
                        name = 'skyway'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/skyway/android-sdk.git'
                    developerConnection = 'scm:git:ssh://github.com:skyway/android-sdk.git'
                    url = 'https://github.com/skyway/android-sdk/'
                }
            }
        }
    }
}
